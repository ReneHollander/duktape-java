apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

def mainClassName = "at.renehollander.duktape.Main"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task fatJar(type: Jar) {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

jar {
    actions = []
    dependsOn = []
    dependsOn('fatJar')
}

task javah(dependsOn: [build]) {
    doLast {
        def classes = [
                'at.renehollander.duktape.Duktape'                  : 'src/main/c/include/duktape-java.h',
                'at.renehollander.duktape.NativeHelper'             : 'src/main/c/include/native-helper.h',
                'at.renehollander.duktape.values.DukReferencedValue': 'src/main/c/include/duk-referenced-value.h',
                'at.renehollander.duktape.values.DukFunction'       : 'src/main/c/include/duk-function.h',
                'at.renehollander.duktape.values.DukObject'         : 'src/main/c/include/duk-object.h',
                'at.renehollander.duktape.values.DukArray'          : 'src/main/c/include/duk-array.h',
                'at.renehollander.duktape.values.DukJavaFunction'   : 'src/main/c/include/duk-java-function.h',
                'at.renehollander.duktape.values.DukError'          : 'src/main/c/include/duk-error.h'
        ]
        classes.each { inputClass, outputFile ->
            // println inputClass + ": " + outputFile
            delete(outputFile)
            ant.javah(class: inputClass, outputFile: outputFile, classpath: sourceSets.main.output.asPath)
        }
    }
}

